Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения) 
	
	Если ОбменДанными.Загрузка Тогда
		
		возврат;
		
	КонецЕсли;	
	
	Если Константы.ЦентральныйМагазин.Получить() = Константы.ТекущийМагазин.Получить() Тогда 
		возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ПроверитьДоступностьВыбранныхСертификатов(Отказ);
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПроверитьДоступностьВыбранныхСертификатов(Отказ) 
	
	СертификатыДокумента = Сертификаты.ВыгрузитьКолонку("Сертификат");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодарочныеСертификаты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ Сертификаты
		|ИЗ
		|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
		|ГДЕ
		|	ПодарочныеСертификаты.Ссылка В(&СертификатыДокумента)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыСертификатовСрезПоследних.Статус КАК Статус,
		|	Сертификаты.Ссылка.Представление КАК ПодарочныйСертификатПредставление
		|ИЗ
		|	Сертификаты КАК Сертификаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСертификатов.СрезПоследних(&Дата, ПодарочныйСертификат В (&СертификатыДокумента)) КАК СтатусыСертификатовСрезПоследних
		|		ПО Сертификаты.Ссылка = СтатусыСертификатовСрезПоследних.ПодарочныйСертификат
		|
		|СГРУППИРОВАТЬ ПО
		|	СтатусыСертификатовСрезПоследних.Статус,
		|	Сертификаты.Ссылка.Представление";
	
	Запрос.УстановитьПараметр("СертификатыДокумента", СертификатыДокумента);
	Запрос.УстановитьПараметр("Дата", КонецДня(ЭтотОбъект.Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	ТекстСообщения = "";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстСообщения = ТекстСообщения + ВыборкаДетальныеЗаписи.ПодарочныйСертификатПредставление + "(Статус "+?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Статус), ВыборкаДетальныеЗаписи.Статус, "не установлен")+")" + Символы.ПС;
	КонецЦикла;
	
	Если ТекстСообщения <> "" Тогда
		Отказ = Истина; 		
		ТекстСообщения = "В документе найдены неподходящие для реализации сертификаты, проведение невозможно 
		|" + ТекстСообщения + ".";
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр СтатусыСертификатов
	Для Каждого ТекСтрокаСертификаты Из Сертификаты Цикл
		НоваяЗапись = РегистрыСведений.СтатусыСертификатов.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период = Дата;
		НоваяЗапись.ПодарочныйСертификат = ТекСтрокаСертификаты.Сертификат;
		НоваяЗапись.Статус = Перечисления.СтатусыПодарочныхСертификатов.Реализован; 
		НоваяЗапись.Записать(Истина);
	КонецЦикла;
	
	// регистр ПодарочныеСертификаты Приход
	Движения.ПодарочныеСертификаты.Записывать = Истина;
	Для Каждого ТекСтрокаСертификаты Из Сертификаты Цикл
		Движение = Движения.ПодарочныеСертификаты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.ПодарочныйСертификат = ТекСтрокаСертификаты.Сертификат;
		Движение.Сумма = ТекСтрокаСертификаты.Сертификат.Сумма;
	КонецЦикла; 

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроверитьДоступностьВыбранныхСертификатовОтмена(Отказ);
	
	Если не Отказ Тогда
	
		Для Каждого ТекСтрокаСертификаты Из Сертификаты Цикл
			
			НаборЗаписей = РегистрыСведений.СтатусыСертификатов.СоздатьНаборЗаписей();

			НаборЗаписей.Отбор.ПодарочныйСертификат.Установить(ТекСтрокаСертификаты.Сертификат);

			НаборЗаписей.Записать();
			
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПроверитьДоступностьВыбранныхСертификатовОтмена(Отказ) 
	
	СертификатыДокумента = Сертификаты.ВыгрузитьКолонку("Сертификат");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодарочныеСертификаты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ Сертификаты
		|ИЗ
		|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
		|ГДЕ
		|	ПодарочныеСертификаты.Ссылка В(&СертификатыДокумента)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СтатусыСертификатовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ПустаяССылка)) КАК Статус,
		|	Сертификаты.Ссылка.Представление КАК ПодарочныйСертификатПредставление
		|ИЗ
		|	Сертификаты КАК Сертификаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСертификатов.СрезПоследних(&Дата, ПодарочныйСертификат В (&СертификатыДокумента)) КАК СтатусыСертификатовСрезПоследних
		|		ПО Сертификаты.Ссылка = СтатусыСертификатовСрезПоследних.ПодарочныйСертификат
		|ГДЕ
		|	ЕСТЬNULL(СтатусыСертификатовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ПустаяССылка)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.Реализован)
		|
		|СГРУППИРОВАТЬ ПО
		|	Сертификаты.Ссылка.Представление,
		|	ЕСТЬNULL(СтатусыСертификатовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.ПустаяССылка))";

		
	
	Запрос.УстановитьПараметр("СертификатыДокумента", СертификатыДокумента);
	Запрос.УстановитьПараметр("Дата", ЭтотОбъект.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	ТекстСообщения = "";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстСообщения = ТекстСообщения + ВыборкаДетальныеЗаписи.ПодарочныйСертификатПредставление + "(Статус "+?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Статус), ВыборкаДетальныеЗаписи.Статус, "не установлен")+")" + Символы.ПС;
	КонецЦикла;
	
	Если ТекстСообщения <> "" Тогда
		Отказ = Истина; 		
		ТекстСообщения = "В документе найдены неподходящие для реализации сертификаты, проведение невозможно 
		|" + ТекстСообщения + ".";
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
	КонецЕсли;
		
КонецПроцедуры
