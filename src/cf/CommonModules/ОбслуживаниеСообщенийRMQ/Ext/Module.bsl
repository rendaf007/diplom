Процедура ОтправитьСообщенияRMQ(Подключение, Очередь) Экспорт    
		
	ТаблицаОчередь = ПолучитьОчередьСообщенийRMQ();	 
	//
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Адрес", Подключение.ИмяСервера);
	СтруктураПараметров.Вставить("Порт", Подключение.Порт);
	СтруктураПараметров.Вставить("Логин", Подключение.Имя);
	СтруктураПараметров.Вставить("Пароль", Подключение.Пароль);
	СтруктураПараметров.Вставить("ВиртуальныйХост", Подключение.ВиртуальныйХост);
	//
	СтруктураПараметров.Вставить("ТочкаОбмена", Очередь.ТочкаОбмена.Наименование);
	СтруктураПараметров.Вставить("ИмяОчереди", Очередь.Очередь);
	СтруктураПараметров.Вставить("КлючМаршрутизации", Очередь.КлючМаршрутизации); 
	  
	Обработки.ОбменRMQ.ОтправитьСообщения(СтруктураПараметров, ТаблицаОчередь);
	
КонецПроцедуры 

Функция ПолучитьОчередьСообщенийRMQ() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СообщенияRMQСрезПоследних.Период КАК Период,
	|	СообщенияRMQСрезПоследних.ОбъектМетаданных КАК ОбъектМетаданных,
	|	СообщенияRMQСрезПоследних.Идентификатор КАК Идентификатор,
	|	СообщенияRMQСрезПоследних.ХранилищеСообщения КАК ХранилищеСообщения,
	|	СообщенияRMQСрезПоследних.Состояние КАК Состояние,
	|	НЕОПРЕДЕЛЕНО КАК Ссылка,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(150)) КАК Метаданные,
	|	СообщенияRMQСрезПоследних.НастройкиМаршрутизации КАК НастройкиМаршрутизации,
	|	СообщенияRMQСрезПоследних.НастройкиМаршрутизации.Очередь КАК ИмяОчереди,
	|	СообщенияRMQСрезПоследних.НастройкиМаршрутизации.ТочкаОбмена.Наименование КАК ТочкаОбмена,
	|	СообщенияRMQСрезПоследних.НастройкиМаршрутизации.КлючМаршрутизации КАК КлючМаршрутизации
	|ИЗ
	|	РегистрСведений.СообщенияRMQ.СрезПоследних(, Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСообщенийRMQ.ЖдетОтправки)) КАК СообщенияRMQСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для Каждого Строка Из РезультатЗапроса Цикл 
		
		Если Найти(Строка.ОбъектМетаданных, "Справочник") Тогда
			Строка.Ссылка = Справочники[СтрЗаменить(Строка.ОбъектМетаданных, "Справочник.", "")].ПолучитьСсылку(Строка.Идентификатор); 
			Строка.Метаданные = "Справочники";
		ИначеЕсли Найти(Строка.ОбъектМетаданных, "Документ") Тогда
            Строка.Ссылка = Документы[СтрЗаменить(Строка.ОбъектМетаданных, "Документ.", "")].ПолучитьСсылку(Строка.Идентификатор); 
			Строка.Метаданные = "Документ";
		КонецЕсли;
		
	КонецЦикла;  
	
	Возврат РезультатЗапроса;
	
КонецФункции 

Функция ОпределитьОчередьRabbitMQПоОбъектуМетаданных(ОбъектМетаданных) Экспорт
	
	ВыборкаДетальныеЗаписи = ПолучитьВыборкуВидовОбъектовДляОбменаRMQ(ОбъектМетаданных);
	
	НастройкиМаршрутизации = Неопределено;  
	ОтборВидНастройки = Новый Структура;
	ОтборВидНастройки.Вставить("ВидНастройки", "Индивидуальное правило");
	НайденаИндвидуальнаяНастройка = ВыборкаДетальныеЗаписи.НайтиСледующий(ОтборВидНастройки); 
	НайденаОбщаяНастрока = Ложь;
	Если НайденаИндвидуальнаяНастройка Тогда
		НастройкиМаршрутизации = ВыборкаДетальныеЗаписи.НастройкиМаршрутизации; 
		ВыборкаДетальныеЗаписи.Сбросить();
	КонецЕсли;
	Если НастройкиМаршрутизации = Неопределено Тогда
		ОтборВидНастройки.Вставить("ВидНастройки", "Общее правило");
		НайденаОбщаяНастрока = ВыборкаДетальныеЗаписи.НайтиСледующий(ОтборВидНастройки);
	КонецЕсли; 
	Если НайденаОбщаяНастрока Тогда   
		НастройкиМаршрутизации = ВыборкаДетальныеЗаписи.НастройкиМаршрутизации;
	КонецЕсли;

  Возврат НастройкиМаршрутизации;  
КонецФункции 

Функция ПолучитьВыборкуВидовОбъектовДляОбменаRMQ(ОбъектМетаданных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ.НастройкиМаршрутизации КАК НастройкиМаршрутизации,
		|	""Индивидуальное правило"" КАК ВидНастройки
		|ИЗ
		|	РегистрСведений.СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ КАК СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ
		|ГДЕ
		|	СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ.ВидОбъекта = &ВидОбъекта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ.НастройкиМаршрутизации,
		|	""Общее правило""
		|ИЗ
		|	РегистрСведений.СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ КАК СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ
		|ГДЕ
		|	СоответствиеВидОбъектаНастройкиМаршрутизацииRMQ.ВидОбъекта = &ВидОбъектаОбщий";
	
	Запрос.УстановитьПараметр("ВидОбъекта", ОбъектМетаданных); 
	Запрос.УстановитьПараметр("ВидОбъектаОбщий", Сред(ОбъектМетаданных, 1, Найти(ОбъектМетаданных, ".") - 1));
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();  
	
	Возврат ВыборкаДетальныеЗаписи;
КонецФункции